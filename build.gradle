plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.google.protobuf' version '0.8.17'
}

group = 'org.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

ext {
    lombokVersion = '1.18.20'
    grpcServerSpringBootStarterVersion = '2.15.0.RELEASE'
    ioGrpcVersion = '1.61.0'
    protocVersion = '3.25.2'
    springCloudVersion = '2023.0.0'
    cucumberVersion = '7.15.0'
    springCloudVersion = "2023.0.0"
}

repositories {
    mavenCentral()
}

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'com.netflix.eureka:eureka-client-jersey3:2.0.2'

    implementation 'org.springframework.boot:spring-boot-starter-graphql'
    implementation 'org.springframework.boot:spring-boot-starter-rsocket'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j' // to enable fallbacks of feign-clients

    implementation "net.devh:grpc-client-spring-boot-starter:${grpcServerSpringBootStarterVersion}"
    implementation "com.google.protobuf:protobuf-java:${protocVersion}"
    implementation "io.grpc:grpc-all:${ioGrpcVersion}"

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    testImplementation 'org.testng:testng:7.9.0'
    testImplementation 'org.mockito:mockito-testng:0.5.2'

    testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-testng:${cucumberVersion}"
    testImplementation 'com.codeborne:selenide:7.1.0'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${ioGrpcVersion}"
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

tasks.named('test') {
    useTestNG()
}
